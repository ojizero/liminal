defmodule Liminal.AccountsTest do
  use Liminal.DataCase

  alias Liminal.Accounts

  import Liminal.AccountsFixtures
  alias Liminal.Accounts.{User, UserToken}

  describe "get_user_by_username/1" do
    test "does not return the user if the username does not exist" do
      refute Accounts.get_user_by_username("unknown_user")
    end

    test "returns the user if the username exists" do
      %{username: username} = user_fixture()
      assert %User{} = Accounts.get_user_by_username(username)
    end
  end

  describe "get_user_by_username_and_password/2" do
    test "does not return the user if the username does not exist" do
      refute Accounts.get_user_by_username_and_password("unknown_user", "hello world!")
    end

    test "does not return the user if the password is not valid" do
      user = user_fixture() |> set_password()
      refute Accounts.get_user_by_username_and_password(user.username, "invalid")
    end

    test "returns the user if the username and password are valid" do
      %{id: id} = user = user_fixture() |> set_password()

      assert %User{id: ^id} =
               Accounts.get_user_by_username_and_password(user.username, valid_user_password())
    end
  end

  describe "get_user!/1" do
    test "raises if id is invalid" do
      assert_raise Ecto.NoResultsError, fn ->
        Accounts.get_user!(Ecto.UUID.generate())
      end
    end

    test "returns the user with the given id" do
      %{id: id} = user = user_fixture()
      assert %User{id: ^id} = Accounts.get_user!(user.id)
    end
  end

  describe "register_user/1" do
    test "requires username to be set" do
      {:error, changeset} = Accounts.register_user(%{})

      assert %{username: ["can't be blank"]} = errors_on(changeset)
    end

    test "validates username format" do
      {:error, changeset} = Accounts.register_user(%{username: "has spaces"})

      assert %{username: ["only letters, numbers, and underscores allowed"]} =
               errors_on(changeset)
    end

    test "validates username length" do
      {:error, changeset} = Accounts.register_user(%{username: "ab"})
      assert "should be at least 3 character(s)" in errors_on(changeset).username
    end

    test "validates username maximum length" do
      too_long = String.duplicate("a", 31)
      {:error, changeset} = Accounts.register_user(%{username: too_long})
      assert "should be at most 30 character(s)" in errors_on(changeset).username
    end

    test "validates username uniqueness" do
      %{username: username} = user_fixture()
      {:error, changeset} = Accounts.register_user(%{username: username})
      assert "has already been taken" in errors_on(changeset).username
    end

    test "validates password when given" do
      {:error, changeset} = Accounts.register_user(%{username: "valid_user", password: "short"})
      assert "should be at least 12 character(s)" in errors_on(changeset).password
    end

    test "validates password confirmation matching" do
      {:error, changeset} =
        Accounts.register_user(%{
          username: "valid_user",
          password: "valid_password_123",
          password_confirmation: "different_password"
        })

      assert "does not match password" in errors_on(changeset).password_confirmation
    end

    test "registers users with confirmed_at set" do
      username = "test_user_123"
      {:ok, user} = Accounts.register_user(%{username: username, password: valid_user_password()})
      assert user.username == username
      assert user.confirmed_at != nil
      assert not is_nil(user.hashed_password)
      assert is_nil(user.password)
    end

    test "creates default tags for the user" do
      username = "test_user_456"
      {:ok, user} = Accounts.register_user(%{username: username, password: valid_user_password()})

      scope = Liminal.Accounts.Scope.for_user(user)
      tags = Liminal.Links.list_tags(scope)
      assert length(tags) == 3

      names = Enum.map(tags, & &1.name) |> Enum.sort()
      assert names == Enum.sort(["saved for later", "read later", "watch later"])
    end
  end

  describe "sudo_mode?/2" do
    test "validates the authenticated_at time" do
      now = DateTime.utc_now()

      assert Accounts.sudo_mode?(%User{authenticated_at: DateTime.utc_now()})
      assert Accounts.sudo_mode?(%User{authenticated_at: DateTime.add(now, -19, :minute)})
      refute Accounts.sudo_mode?(%User{authenticated_at: DateTime.add(now, -21, :minute)})

      # minute override
      refute Accounts.sudo_mode?(
               %User{authenticated_at: DateTime.add(now, -11, :minute)},
               -10
             )

      # not authenticated
      refute Accounts.sudo_mode?(%User{})
    end
  end

  describe "change_user_password/3" do
    test "returns a user changeset" do
      assert %Ecto.Changeset{} = changeset = Accounts.change_user_password(%User{})
      assert changeset.required == [:password]
    end

    test "allows fields to be set" do
      changeset =
        Accounts.change_user_password(
          %User{},
          %{
            "password" => "new valid password"
          },
          hash_password: false
        )

      assert changeset.valid?
      assert get_change(changeset, :password) == "new valid password"
      assert is_nil(get_change(changeset, :hashed_password))
    end
  end

  describe "update_user_password/2" do
    setup do
      %{user: user_fixture()}
    end

    test "validates password", %{user: user} do
      {:error, changeset} =
        Accounts.update_user_password(user, %{
          password: "not valid",
          password_confirmation: "another"
        })

      assert %{
               password: ["should be at least 12 character(s)"],
               password_confirmation: ["does not match password"]
             } = errors_on(changeset)
    end

    test "validates maximum values for password for security", %{user: user} do
      too_long = String.duplicate("db", 100)

      {:error, changeset} =
        Accounts.update_user_password(user, %{password: too_long})

      assert "should be at most 72 character(s)" in errors_on(changeset).password
    end

    test "updates the password", %{user: user} do
      {:ok, {user, expired_tokens}} =
        Accounts.update_user_password(user, %{
          password: "new valid password"
        })

      assert expired_tokens == []
      assert is_nil(user.password)
      assert Accounts.get_user_by_username_and_password(user.username, "new valid password")
    end

    test "deletes all tokens for the given user", %{user: user} do
      _ = Accounts.generate_user_session_token(user)

      {:ok, {_, _}} =
        Accounts.update_user_password(user, %{
          password: "new valid password"
        })

      refute Repo.get_by(UserToken, user_id: user.id)
    end
  end

  describe "generate_user_session_token/1" do
    setup do
      %{user: user_fixture()}
    end

    test "generates a token", %{user: user} do
      token = Accounts.generate_user_session_token(user)
      assert user_token = Repo.get_by(UserToken, token: token)
      assert user_token.context == "session"
      assert user_token.authenticated_at != nil

      # Creating the same token for another user should fail
      assert_raise Ecto.ConstraintError, fn ->
        Repo.insert!(%UserToken{
          token: user_token.token,
          user_id: user_fixture().id,
          context: "session"
        })
      end
    end

    test "duplicates the authenticated_at of given user in new token", %{user: user} do
      user = %{user | authenticated_at: DateTime.add(DateTime.utc_now(:second), -3600)}
      token = Accounts.generate_user_session_token(user)
      assert user_token = Repo.get_by(UserToken, token: token)
      assert user_token.authenticated_at == user.authenticated_at
      assert DateTime.compare(user_token.inserted_at, user.authenticated_at) == :gt
    end
  end

  describe "get_user_by_session_token/1" do
    setup do
      user = user_fixture()
      token = Accounts.generate_user_session_token(user)
      %{user: user, token: token}
    end

    test "returns user by token", %{user: user, token: token} do
      assert {session_user, token_inserted_at} = Accounts.get_user_by_session_token(token)
      assert session_user.id == user.id
      assert session_user.authenticated_at != nil
      assert token_inserted_at != nil
    end

    test "does not return user for invalid token" do
      refute Accounts.get_user_by_session_token("oops")
    end

    test "does not return user for expired token", %{token: token} do
      dt = ~N[2020-01-01 00:00:00]
      {1, nil} = Repo.update_all(UserToken, set: [inserted_at: dt, authenticated_at: dt])
      refute Accounts.get_user_by_session_token(token)
    end
  end

  describe "delete_user_session_token/1" do
    test "deletes the token" do
      user = user_fixture()
      token = Accounts.generate_user_session_token(user)
      assert Accounts.delete_user_session_token(token) == :ok
      refute Accounts.get_user_by_session_token(token)
    end
  end

  describe "change_user_username/3" do
    test "returns a changeset" do
      assert %Ecto.Changeset{} = Accounts.change_user_username(%User{})
    end

    test "validates username format" do
      changeset = Accounts.change_user_username(%User{}, %{"username" => "has spaces"})

      assert %{username: ["only letters, numbers, and underscores allowed"]} =
               errors_on(changeset)
    end

    test "validates username length" do
      changeset = Accounts.change_user_username(%User{}, %{"username" => "ab"})
      assert %{username: ["should be at least 3 character(s)"]} = errors_on(changeset)
    end
  end

  describe "update_user_username/2" do
    setup do
      %{user: user_fixture()}
    end

    test "updates the username", %{user: user} do
      {:ok, updated} = Accounts.update_user_username(user, %{"username" => "new_name"})
      assert updated.username == "new_name"
    end

    test "returns error changeset on invalid username", %{user: user} do
      assert {:error, changeset} =
               Accounts.update_user_username(user, %{"username" => "bad name!"})

      assert %{username: [_]} = errors_on(changeset)
    end
  end

  describe "inspect/2 for the User module" do
    test "does not include password" do
      refute inspect(%User{password: "123456"}) =~ "password: \"123456\""
    end
  end
end
